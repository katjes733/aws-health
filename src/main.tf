terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 3.27"
    }
  }

  required_version = ">= 0.14.9"
}

provider "aws" {
  profile = "rearc_bloomberg"
  region  = "us-east-1"
}

variable "resource_prefix" {
    description = "The prefix for all resources. If empty, auto generated by AWS including the name of the stack."
    type        = string
    default     = "mac-"

    validation {
        condition     = can(regex("^$|^[a-z0-9-]{0,7}$", var.resource_prefix))
        error_message = "The resource_prefix must be empty or not be longer that 7 characters containing only the following characters: a-z0-9- ."
    }
}

variable "teams_hook_url" {
    description = "The Teams Hook URL"
    type        = string
    default     = ""

    validation {
        condition     = can(regex("^$|^https:\\/\\/[a-zA-Z0-9_\\-\\+]+(\\.[a-zA-Z0-9_\\-\\+]+)+\\/.+$", var.teams_hook_url))
        error_message = "The teams_hook_url must be empty or a valid URL beginning with https:// ."
    }
}

variable "slack_hook_url" {
    description = "The Slack Hook URL"
    type        = string
    default     = ""

    validation {
        condition     = can(regex("^$|^https:\\/\\/[a-zA-Z0-9_\\-\\+]+(\\.[a-zA-Z0-9_\\-\\+]+)+\\/.+$", var.slack_hook_url))
        error_message = "The slack_hook_url must be empty or a valid URL beginning with https:// ."
    }
}

variable "execution_rate" {
    description = "The execution rate in minutes"
    type        = number
    default     = 10

    validation {
        condition     = var.execution_rate >= 1 && var.execution_rate <= 60
        error_message = "The execution_rate must be between 1 and 60 minutes."
    }
}

data "aws_partition" "current" {}

data "aws_region" "current" {}

locals {
    is_arm_supported_region = contains(["us-east-1", "us-west-2", "eu-central-1", "eu-west-1", "ap-south-1", "ap-southeast-1", "ap-southeast-2", "ap-northeast-1"], data.aws_region.current.name)
    aws_health_notification_lambda_function_name = "%{ if var.resource_prefix != "" }${var.resource_prefix}%{ else }${random_string.unique_id}-%{ endif }AwsHealthNotification"
}

resource "random_string" "unique_id" {
    count   = var.resource_prefix == "" ? 1 : 0
    length  = 8
    special = false  
}

resource "aws_iam_role" "aws_health_notification_lambda_role" {
    name = var.resource_prefix != "" ? "${var.resource_prefix}AwsHealthNotificationLambdaRole" : null
    managed_policy_arns = [
        "arn:${data.aws_partition.current.partition}:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole",
        aws_iam_policy.aws_health_notification_lambda_role_policy.arn
    ]
    assume_role_policy = jsonencode({
        Version = "2012-10-17"
        Statement = [
            {
                Action = "sts:AssumeRole"
                Effect = "Allow"
                Principal = {
                    Service = "lambda.amazonaws.com"
                }
            },
        ]
    })
}

resource "aws_iam_policy" "aws_health_notification_lambda_role_policy" {
    name = "%{ if var.resource_prefix != "" }${var.resource_prefix}%{ else }${random_string.unique_id}-%{ endif }AwsHealthNotificationLambdaRolePolicy"
    policy = jsonencode({
        Version = "2012-10-17"
        Statement = [
            {
                Action   = ["health:DescribeEvents"]
                Effect   = "Allow"
                Resource = "*"
            },
            {
                Action   = ["health:DescribeEventDetails"]
                Effect   = "Allow"
                Resource = "arn:aws:health:*::event/*/*/*"
            },
        ]
    })
}

resource "aws_cloudwatch_log_group" "aws_health_notification_lambda_log_group" {
    name = "/aws/lambda/${local.aws_health_notification_lambda_function_name}"
    retention_in_days = 7
}

data "archive_file" "aws_health_package" {
    type = "zip"
    source_dir = "${path.module}/python/"
    output_path = "${path.module}/.package/aws_health_package.zip"
}

resource "aws_lambda_function" "aws_health_notification_lambda" {
    depends_on = [
        aws_cloudwatch_log_group.aws_health_notification_lambda_log_group
    ]
    function_name = "${local.aws_health_notification_lambda_function_name}"
    architectures = local.is_arm_supported_region ? ["arm64"] : ["x86_64"]
    filename = "${path.module}/.package/aws_health_package.zip"
    handler = "index.lambda_handler"
    runtime = "python3.9"
    memory_size = 128
    timeout = 60
    role = aws_iam_role.aws_health_notification_lambda_role.arn
    environment {
      variables = {
        "TeamsHookUrl" = "${var.teams_hook_url}",
        "SlackHookUrl" = "${var.slack_hook_url}",
        "CheckTime" = "${var.execution_rate}"
      }
    }
}

resource "aws_cloudwatch_event_rule" "aws_health_notification_event_rule_on_schedule" {
    name = "%{ if var.resource_prefix != "" }${var.resource_prefix}%{ else }${random_string.unique_id}-%{ endif }AwsHealthNotificationEventRuleOnSchedule"
    description = "Scheduled rule to trigger check for AWS Health"
    schedule_expression = "rate(${var.execution_rate} %{ if var.execution_rate == 1 }minute%{ else }minutes%{ endif })"    
}

resource "aws_cloudwatch_event_target" "aws_health_notification_event_rule_on_schedule_target" {
    rule      = "${aws_cloudwatch_event_rule.aws_health_notification_event_rule_on_schedule.name}"
    target_id = "%{ if var.resource_prefix != "" }${var.resource_prefix}%{ else }${random_string.unique_id}-%{ endif }AwsHealthNotificationEventRuleOnScheduleTarget"
    arn       = "${aws_lambda_function.aws_health_notification_lambda.arn}"
}

resource "aws_lambda_permission" "aws_health_notification_lambda_permission" {
    statement_id  = "%{ if var.resource_prefix != "" }${var.resource_prefix}%{ else }${random_string.unique_id}-%{ endif }AwsHealthNotificationLambdaPermission"
    action        = "lambda:InvokeFunction"
    function_name = "${aws_lambda_function.aws_health_notification_lambda.function_name}"
    principal     = "events.amazonaws.com"
    source_arn    = "${aws_cloudwatch_event_rule.aws_health_notification_event_rule_on_schedule.arn}"
}
